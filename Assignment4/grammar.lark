?start: prog

?prog: exp
     | exp ";;" prog -> seq

?exp: sum
    | "let" NAME "=" exp "in" exp    -> let
    | "letrec" NAME "=" exp "in" exp -> rec
    | "fix" exp                      -> fix
    | "if" exp "then" exp "else" exp -> if_then_else
    | exp8

?exp8: "hd" exp     -> hd
     | "tl" exp     -> tl
     | exp ":" exp -> cons

?sum: product
    | sum "+" product    -> plus
    | sum "-" product    -> minus

?product: comparison
        | product "*" comparison    -> multiply

?comparison: unary
          | comparison "==" unary  -> eq
          | comparison "<" unary   -> lt
          | comparison ">" unary   -> gt
          | comparison "<=" unary  -> le
          | comparison ">=" unary  -> ge

?unary: app
      | "-" unary         -> negation

?app: atom
    | app atom           -> app

?atom: NUMBER            -> number
     | NAME              -> var
     | "\\" NAME "." exp -> lam
     | "(" exp ")"
     | "#"               -> nil

// Define the terminals (tokens)
WS: /\s+/
COMMENT: /\/\/.*/

NAME: /[a-z_][a-zA-Z0-9_]*/
NUMBER: /\d+(\.\d+)?/

%ignore WS
%ignore COMMENT