?start: exp

?exp: sum

?sum: product
    | sum "+" product    -> plus
    | sum "-" product    -> minus

?product: unary
        | product "*" unary    -> multiply

?unary: app
      | "-" unary         -> negation

?app: atom
    | app atom           -> app

?atom: NUMBER            -> number
     | NAME              -> var
     | "\\" NAME "." exp -> lam
     | "(" exp ")"

// Define the terminals (tokens)
WS: /\s+/
COMMENT: /\/\/.*/

NAME: /[a-z_][a-zA-Z0-9_]*/
NUMBER: /\d+(\.\d+)?/

%ignore WS
%ignore COMMENT